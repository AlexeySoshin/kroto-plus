apply plugin: 'application'

description 'Kroto+ Compiler CLI'
mainClassName = 'com.github.marcoferrer.krotoplus.KrotoPlusCompilerMain'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Implementation-Title' : 'Kroto+ Compiler'
        attributes 'Implementation-Version': project.version
    }
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {

    //Currently served from Kroto+ bintray
    implementation "org.jetbrains.kotlinx:kotlinx-cli-jvm:0.1.0"

    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"

    implementation "com.squareup.wire:wire-schema:2.2.0"
    implementation ("com.squareup:kotlinpoet:0.7.0"){
        exclude group: 'org.jetbrains.kotlin'
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            pom.withXml {
                // Dependencies are embedded into the jar.
                // Remove dependencies from pom output.
                def root = asNode()
                root.remove(root.dependencies)
            }
        }
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/kotlin/main")
        testOutputDir = file("$buildDir/classes/kotlin/test")
    }
}