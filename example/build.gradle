buildscript{
    repositories {
        jcenter()
        mavenCentral()
        flatDir dirs: "../gradle-plugin/build/libs/"
        flatDir dirs: "../compiler/build/libs/"
    }

    dependencies{
        classpath group: 'com.github.mferrer.krotoplus', name: 'gradle-plugin', version: '0.1.0-SNAPSHOT'
        classpath group: 'com.github.mferrer.krotoplus', name: 'compiler', version: '0.1.0-SNAPSHOT'
    }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.mferrer.kroto-plus'

def generatedOutputDir = "$buildDir/generated-sources/main/kotlin"

sourceSets {
    main.kotlin.srcDirs += generatedOutputDir

    main {
        java {
            // Add additional source directory for generated sources
            srcDir generatedOutputDir
        }
    }
}

dependencies {
    compile "com.google.protobuf:protobuf-java:${versions.protobuf}"
    compile project(':runtime')
    compile project(':runtime-test')

    compile "io.grpc:grpc-protobuf:${versions.grpc}",
            "io.grpc:grpc-stub:${versions.grpc}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${versions.protoc}"}

    generatedFilesBaseDir = "$buildDir/generated-sources"

    //noinspection GroovyAssignabilityCheck
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${versions.grpc}" }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc { outputSubDir = "java" }
        }
    }
}

krotoPlus{
    //Proto definition source directories, or path to jar containing proto definitions
    sources = [
        "$projectDir/src/main/proto",
        "$buildDir/extracted-include-protos/main"
    ]
    outputDir = "${protobuf.generatedFilesBaseDir}/main/kotlin"
}

generateKrotoPlus.dependsOn(':gradle-plugin:jar')