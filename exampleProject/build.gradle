buildscript{
    ext {
        versions = [
                "protoc":     '3.5.1',
                "protobuf":   '3.5.1',
                "grpc":       '1.10.0',
                "kotlin":     '1.2.21',
                "coroutines": '0.22.5',
                "krotoplus": '0.1.0-SNAPSHOT'
        ]
    }

    repositories {
        jcenter()
        mavenCentral()
        flatDir dirs: "../gradle-plugin/build/libs/"
        flatDir dirs: "../compiler/build/libs/"
    }

    dependencies{
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
        classpath "com.github.mferrer.krotoplus:gradle-plugin:${versions.krotoplus}"
        classpath "com.github.mferrer.krotoplus:compiler:${versions.krotoplus}"
    }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.mferrer.kroto-plus'
apply plugin: 'idea'
apply plugin: 'kotlin'

group = 'com.github.mferrer.krotoplus'
version = '0.1.0-SNAPSHOT'

kotlin {
    experimental { coroutines 'enable' }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    jcenter()
    mavenCentral()
    flatDir dirs: "../runtime/build/libs/"
    flatDir dirs: "../runtime-test/build/libs/"
}


def generatedOutputDir = "$buildDir/generated-sources/main/kotlin"

sourceSets {
    main.kotlin.srcDirs += generatedOutputDir

    main {
        java {
            // Add additional source directory for generated sources
            srcDir generatedOutputDir
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
    compile "com.google.protobuf:protobuf-java:${versions.protobuf}"
    compile "com.github.mferrer.krotoplus:runtime:${versions.krotoplus}"
    compile "com.github.mferrer.krotoplus:runtime-test:${versions.krotoplus}"

    compile "io.grpc:grpc-protobuf:${versions.grpc}",
            "io.grpc:grpc-stub:${versions.grpc}"

    testCompile "io.grpc:grpc-testing:${versions.grpc}"

    testCompile group: 'junit', name: 'junit', version: '[4,)'
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${versions.protoc}"}

    generatedFilesBaseDir = "$buildDir/generated-sources"

    //noinspection GroovyAssignabilityCheck
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${versions.grpc}" }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc { outputSubDir = "java" }
        }
    }
}

krotoPlus{
    //Proto definition source directories, or path to jar containing proto definitions
    sources = [
        "$projectDir/src/main/proto",
        "$buildDir/extracted-include-protos/main"
    ]
    outputDir = "${protobuf.generatedFilesBaseDir}/main/kotlin"
}

task generateKrotoPlusCLI {
    doLast {
        javaexec {
            main = '-jar'
            args = [
                    //Path service generator fat jar
                    "/Users/mferrer/IdeaProjects/kroto-plus/compiler/build/libs/compiler-0.1.0-SNAPSHOT.jar",

                    //Proto definition source directories or path to jar
                    "$projectDir/src/main/proto", "$buildDir/extracted-include-protos/main",

                    //Generated sources output directory
                    "-o", generatedOutputDir
            ]
        }
    }
}